C
C***********************************************************************
C                                                                      *
C            NACHOS II - A Finite Element Computer Program             *
C                        for Incompressible Flow Problems              *
C                                                                      *
C     Copyright (c) 1986,2019   National Technology & Engineering      *
C                               Solutions of Sandia, LLC (NTESS)       *
C                                                                      *
C                            All rights reserved.                      *
C                                                                      *
C     This software is distributed under the BSD 3-Clause License.     *
C                                                                      *
C***********************************************************************
C
      SUBROUTINE GRPHFL (XG,YG,ICON,LISTEL,UN,USAVE,ISEVOK,ITEST1,      
     1ITEST2)
C
C     ******************************************************************
C
C     SUBROUTINE TO CREATE A POST-PROCESSING FILE
C     DATA IS WRITTEN TO NTP12=UNIT 22 IN THE 'EXODUS' FILE FORMAT
C     THE FIRST PART OF THE 'EXODUS' FILE CONSISTS OF THE 'GENESIS'
C     INPUT FILE, WHICH WAS PREVIOUSLY WRITTEN TO NTP12
C
C     ******************************************************************
C
      CHARACTER*8  NAME,NAMECO,NAMEEL,NAMEHV,NAMEGV,NAMENV,NAMEEV
      CHARACTER*8  CODNAM,VERSN,RDATE,RTIME,HRDWRE,SFTWRE
      CHARACTER*10 CDATA
C
      COMMON /RUNDAT/ CODNAM,VERSN,RDATE,RTIME,HRDWRE,SFTWRE
      COMMON /INDATR/ RDATA(150)
      COMMON /INDATI/ IDATA(150)
      COMMON /INDATC/ CDATA(150)
      COMMON /TAPES/  NIN,NOUT,NTP0,NTP1,NTP2,NTP3,NTP4,NTP5,NTP6,NTP7, 
     1                NTP8,NTP9,NTP10,NTP11,NTP12,NTP13
      COMMON /SZDAT/  NUMEL,NUMNOD,NUMVAR,NUMDOF,NODSOL
      COMMON /LMTDAT/ MAXELM,MAXNOD,MAXBC,MAXEDG,MAXMAT,MAXNPT,MAXPLT,  
     1                MAXBRY,MAXBLK
      COMMON /GENDAT/ NELBLK,NUMNPS,LNPSNL,NUMESS,LESSEL,LESSNL,MXEBLK, 
     1                IBLK(100)
      COMMON /MATDAT/ NMAT,NPROP,PROP(15,10),NXMAT,NXPROP,XPROP(15,10)
      COMMON /PRBDAT/ IAXSYM,ITMDEP,IFORCE,IFREE,IVAR1,IVAR2,IPFUNC,    
     1                IPNLTY,PNLTY
      COMMON /SPTDAT/ ISPT,NSPT,NELSPT(50),PTS(50,2),SPTVAL(50,6)
C
      DIMENSION XG(*), YG(*), LISTEL(*)
      DIMENSION ICON(NUMEL,*), UN(NUMNOD,*), USAVE(NUMNOD,*)
      DIMENSION ISEVOK(10,*)
      DIMENSION NAMECO(2), NAMEEL(6)
      DIMENSION NAMEHV(10), NAMEGV(10), NAMENV(20), NAMEEV(10)
      DIMENSION NAME(100)
      DIMENSION IHISV(10), IGLBV(10), INODV(20), IELV(10)
      DIMENSION NTMPLN(50)
C
      DATA NCORD,NVARHI,NVARGL,NVARNP,NVAREL/2,0,0,0,0/
      DATA (NAMECO(I),I=1,2)/'X','Y'/
      DATA (NAMEEL(I),I=1,6)/'TRIANGLE','TRIANGLE','QUAD','QUAD',       
     1     'QUAD','QUAD'/
      DATA (NAMEHV(I),I=1,10)/'UVEL*','VVEL*','PRESS*','TEMP*',         
     1     'AUXVAR1*','AUXVAR2*','*','*','*','*'/
      DATA (NAMEGV(I),I=1,10)/'*','*','*','*','*','*','*','*','*','*'/
      DATA (NAMENV(I),I=1,20)/'UVEL','VVEL','PRESS','TEMP','AUXVAR1',   
     1     'AUXVAR2','STREAM','TAUX','TAUY','TAUXY','TAUTHETA','VORT',  
     2     'QX','QY','V1X','V1Y','V2X','V2Y','*','*'/
      DATA (NAMEEV(I),I=1,10)/'*','*','*','*','*','*','*','*','*','*'/
      DATA (IHISV(I),I=1,10)/10*0/
      DATA (IGLBV(I),I=1,10)/10*0/
      DATA (INODV(I),I=1,20)/20*0/
      DATA (IELV(I),I=1,10)/10*0/
      DATA NQA,NINFO/1,0/
      DATA HIFLAG/0./
      DATA DUMWRT/0./
C
C     ******************************************************************
C
C     SET PARAMETERS FOR WRITING FILE
C
      WRITE (NOUT, 370)
      ICARD1=0
      ICARD2=0
   10 CONTINUE
      CALL RDFFLD (NIN,RDATA,IDATA,CDATA)
      IF (CDATA(1).EQ.'END       ') GO TO 150
      IF (CDATA(1).EQ.'HISTORY   ') GO TO 20
      IF (CDATA(1).EQ.'GLOBAL    ') GO TO 40
      IF (CDATA(1).EQ.'NODES     ') GO TO 60
      IF (CDATA(1).EQ.'ELEMENTS  ') GO TO 80
      IF (CDATA(1).EQ.'TIMEPLANE ') GO TO 100
      CALL ERROR ('GRPHFL','UNRECOGNIZED DATA WORD IN POST COMMAND',' ',
     10,' ',0,'WORD',CDATA(1),1)
C
   20 CONTINUE
      NVARHI=IDATA(2)
      DO 30 I=1,NVARHI
      DO 30 J=1,10
      IF (CDATA(I+2).EQ.NAMEHV(J)) IHISV(J)=1
   30 CONTINUE
      ICARD1=ICARD1+1
      GO TO 10
C
   40 CONTINUE
      NVARGL=IDATA(2)
      DO 50 I=1,NVARGL
      DO 50 J=1,10
      IF (CDATA(I+2).EQ.NAMEGV(J)) IGLBV(J)=1
   50 CONTINUE
      ICARD1=ICARD1+1
      GO TO 10
C
   60 CONTINUE
      NVARNP=IDATA(2)
      DO 70 I=1,NVARNP
      DO 70 J=1,20
      IF (CDATA(I+2).EQ.NAMENV(J)) INODV(J)=1
   70 CONTINUE
      ICARD1=ICARD1+1
      GO TO 10
C
   80 CONTINUE
      NVAREL=IDATA(2)
      DO 90 I=1,NVAREL
      DO 90 J=1,10
      IF (CDATA(I+2).EQ.NAMEEV(J)) IELV(J)=1
   90 CONTINUE
      ICARD1=ICARD1+1
      GO TO 10
C
  100 CONTINUE
      ICARD2=1
      IF (CDATA(2).EQ.'ALL       ') GO TO 110
      IF (CDATA(2).EQ.'INCREMENT ') GO TO 120
      IF (CDATA(2).EQ.'SPECIFIED ') GO TO 130
  110 CONTINUE
      INCL=1
      INCU=10000
      INC=1
      ITIME=1
      WRITE (NOUT, 380)
      GO TO 10
  120 CONTINUE
      INCL=IDATA(3)
      IF (INCL.EQ.0) INCL=1
      INCU=IDATA(4)
      IF (INCU.EQ.0) INCU=10000
      INC=IDATA(5)
      IF (INC.EQ.0) INC=1
      ITIME=1
      WRITE (NOUT, 390) INCL,INCU,INC
      GO TO 10
  130 CONTINUE
      NTIMES=IDATA(3)
      IF (NTIMES.GT.50) NTIMES=50
      IF (NTIMES.EQ.0) GO TO 110
      DO 140 I=1,NTIMES
      NTMPLN(I)=IDATA(I+3)
  140 CONTINUE
      ITIME=0
      WRITE (NOUT, 400) NTIMES,(NTMPLN(I),I=1,NTIMES)
      GO TO 10
C
  150 CONTINUE
      IF (ICARD1.EQ.0) CALL ERROR ('GRPHFL','VARIABLE DATA CARDS NOT FOU
     1ND FOR POST COMMAND',' ',0,' ',0,' ',' ',1)
      IF (ICARD2.EQ.0) CALL ERROR ('GRPHFL','TIMEPLANE DATA CARD NOT FOU
     1ND FOR POST COMMAND',' ',0,' ',0,' ',' ',1)
      IF (ITEST1.EQ.0.AND.INODV(7).EQ.1) THEN
      NVARNP=NVARNP-INODV(7)
      INODV(7)=0
      WRITE (NOUT, 410)
      END IF
      IF (ITEST2.EQ.0.AND.(INODV(8)+INODV(9)+INODV(10)+INODV(11)+       
     1INODV(12)+INODV(13)+INODV(14)+INODV(15)+INODV(16)+INODV(17)+      
     2INODV(18)).GT.0) THEN
      NVARNP=NVARNP-INODV(8)-INODV(9)-INODV(10)-INODV(11)-INODV(12)-    
     1INODV(13)-INODV(14)-INODV(15)-INODV(16)-INODV(17)-INODV(18)
      INODV(8)=0
      INODV(9)=0
      INODV(10)=0
      INODV(11)=0
      INODV(12)=0
      INODV(13)=0
      INODV(14)=0
      INODV(15)=0
      INODV(16)=0
      INODV(17)=0
      INODV(18)=0
      WRITE (NOUT, 420)
      END IF
C
C     WRITE PART A OF EXODUS OUTPUT FILE
C
C     QA INFORMATION RECORD
C
      WRITE (NTP12) NQA
      WRITE (NTP12) CODNAM,VERSN,RDATE,RTIME
C
C     OPTIONAL TEXT RECORDS
C
      WRITE (NTP12) NINFO
      DO 160 I=1,NINFO
      WRITE (NTP12) DUMWRT
  160 CONTINUE
C
C     COORDINATE NAME RECORD
C
      IF (IAXSYM.EQ.1) THEN
      NAMECO(1)='R'
      NAMECO(2)='Z'
      END IF
      WRITE (NTP12) (NAMECO(I),I=1,NCORD)
C
C     ELEMENT NAME RECORD
C
      DO 170 I=1,NELBLK
      NN=IBLK(I)
      KIND=MOD(NN,100)
      NAME(I)=NAMEEL(KIND)
  170 CONTINUE
      WRITE (NTP12) (NAME(I),I=1,NELBLK)
C
C     WRITE PART B OF EXODUS OUTPUT FILE
C
C     OUTPUT VARIABLE SIZING RECORD
C
      WRITE (NTP12) NVARHI,NVARGL,NVARNP,NVAREL
C
C     OUTPUT VARIABLE NAME RECORD
C
      IF (NVARHI.EQ.0) GO TO 190
      II=0
      DO 180 I=1,10
      IF (IHISV(I).EQ.1) THEN
      II=II+1
      NAME(II)=NAMEHV(I)
      END IF
  180 CONTINUE
      WRITE (NOUT, 430) NVARHI,(NAME(I),I=1,NVARHI)
  190 CONTINUE
C
      IF (NVARGL.EQ.0) GO TO 210
      II=20
      DO 200 I=1,10
      IF (IGLBV(I).EQ.1) THEN
      II=II+1
      NAME(II)=NAMEGV(I)
      END IF
  200 CONTINUE
      WRITE (NOUT, 460) NVARGL,(NAME(I),I=21,20+NVARGL)
  210 CONTINUE
C
      IF (NVARNP.EQ.0) GO TO 230
      II=40
      DO 220 I=1,20
      IF (INODV(I).EQ.1) THEN
      II=II+1
      NAME(II)=NAMENV(I)
      END IF
  220 CONTINUE
      WRITE (NOUT, 440) NVARNP,(NAME(I),I=41,40+NVARNP)
  230 CONTINUE
C
      IF (NVAREL.EQ.0) GO TO 250
      II=60
      DO 240 I=1,10
      IF (IELV(I).EQ.1) THEN
      II=II+1
      NAME(II)=NAMEEV(I)
      END IF
  240 CONTINUE
      WRITE (NOUT, 450) NVAREL,(NAME(I),I=61,60+NVAREL)
  250 CONTINUE
C
      WRITE (NTP12) (NAME(I),I=1,NVARHI),(NAME(I),I=21,20+NVARGL),      
     1(NAME(I),I=41,40+NVARNP),(NAME(I),I=61,60+NVAREL)
C
C     TRUTH TABLE FOR ELEMENT VARIABLES (UNUSED)
C
      DO 260 I=1,NVAREL
      DO 260 J=1,NELBLK
      ISEVOK(I,J)=0
  260 CONTINUE
      WRITE (NTP12) ((ISEVOK(I,J),I=1,NVAREL),J=1,NELBLK)
C
C     SOLUTION RECORD(S)
C
      IF (ITIME.EQ.1) THEN
      LSUM=INCL
      LIMIT=INCL
       ELSE
      II=0
      LIMIT=NTMPLN(1)
      END IF
      NREC=0
  270 CONTINUE
C
C     READ SOLUTION FILE
C
      DO 280 L=1,LIMIT
      READ (NTP9, END=360) TIME,NUMNOD,NUMVAR,((UN(I,J),I=1,NUMNOD),    
     1  J=1,NUMVAR)
  280 CONTINUE
C
C     WRITE SOLUTION FILE IN EXODUS FORMAT
C
      WRITE (NTP12) TIME,HIFLAG
C
C     HISTORY VARIABLE RECORD
C
C     NO HISTORY VARIABLES CURRENTLY USED IN NACHOS
C
      WRITE (NTP12) DUMWRT
C
C     GLOBAL VARIABLE RECORD
C
C     NO GLOBAL VARIABLES CURRENTLY USED IN NACHOS
C
      WRITE (NTP12) DUMWRT
C
C     NODAL POINT VARIABLE RECORDS
C
      DO 290 J=1,NUMVAR
      IF (INODV(J).EQ.0) GO TO 290
      WRITE (NTP12) (UN(I,J),I=1,NUMNOD)
  290 CONTINUE
C
      IF (INODV(7).EQ.1) THEN
      DO 300 L=1,LIMIT
      READ (NTP5, END=360) TIME,NUMNOD,NPSI,(UN(I,1),I=1,NUMNOD)
  300 CONTINUE
      WRITE (NTP12) (UN(I,1),I=1,NUMNOD)
      END IF
C
      IF (INODV(8)+INODV(9)+INODV(10)+INODV(11)+INODV(12).GT.0) THEN
      DO 310 L=1,LIMIT
      READ (NTP8, END=360) TIME,NUMNOD,NVAR,((USAVE(I,J),I=1,NUMNOD),   
     1  J=1,NVAR)
  310 CONTINUE
      DO 320 J=1,NVAR
      IF (INODV(J+7).EQ.0) GO TO 320
      WRITE (NTP12) (USAVE(I,J),I=1,NUMNOD)
  320 CONTINUE
      END IF
C
      IF (INODV(13)+INODV(14)+INODV(15)+INODV(16)+INODV(17)+INODV(18)   
     1.GT.0) THEN
      DO 330 L=1,LIMIT
      READ (NTP7, END=360) TIME,NUMNOD,NVAR,((USAVE(I,J),I=1,NUMNOD),   
     1  J=1,NVAR)
  330 CONTINUE
      DO 340 J=1,NVAR
      IF (INODV(J+12).EQ.0) GO TO 340
      WRITE (NTP12) (USAVE(I,J),I=1,NUMNOD)
  340 CONTINUE
      END IF
C
C     ELEMENT VARIABLE RECORD
C
C     NO ELEMENT DATA CURRENTLY USED IN NACHOS
C
      DO 350 I=1,NELBLK
      DO 350 J=1,NVAREL
      IF (ISEVOK(J,I).NE.0) THEN
      WRITE (NTP12) DUMWRT
      END IF
  350 CONTINUE
C
C     CHECK FOR NEXT TIMEPLANE
C
      NREC=NREC+1
      IF (ITIME.EQ.1) THEN
      LIMIT=INC
      LSUM=LSUM+INC
      IF (LSUM.GT.INCU) GO TO 360
      GO TO 270
       ELSE
      II=II+1
      IF (II.EQ.NTIMES) GO TO 360
      LIMIT=NTMPLN(II)-NTMPLN(II-1)
      END IF
C
  360 CONTINUE
      WRITE (NOUT, 470) NREC
      RETURN
C
  370 FORMAT (//,3X,'POST-PROCESSING GRAPHICS FILE REQUESTED')
  380 FORMAT (//,3X,'TIMEPLANES TO BE INCLUDED IN FILE -',/,3X,'ALL')
  390 FORMAT (//,3X,'TIMEPLANES TO BE INCLUDED IN FILE -',/,3X,         
     1'FIRST TIMEPLANE - ',I4,3X,'LAST TIMEPLANE - ',I4,3X,             
     2'INCREMENT - ',I4)
  400 FORMAT (//,3X,'TIMEPLANES TO BE INCLUDED IN FILE - ',/,3X,I2,     
     1' TIMEPLANES - ',/,10I5,4(/,28X,10I5))
  410 FORMAT (//,3X,'***** WARNING *****',//,3X,'STREAM FUNCTION NOT AVA
     1ILABLE FOR INCLUSION IN GRAPHICS FILE')
  420 FORMAT (//,3X,'***** WARNING *****',//,3X,'FLUX VARIABLES NOT AVAI
     1LABLE FOR INCLUSION IN GRAPHICS FILE')
  430 FORMAT (//,3X,I2,' HISTORY VARIABLES INCLUDED IN FILE',/,3X,      
     1'REQUESTED VARIABLES INCLUDE -'/,10(/,3X,A8))
  440 FORMAT (//,3X,I2,' NODAL POINT VARIABLES INCLUDED IN FILE',/,3X,  
     1'REQUESTED VARIABLES INCLUDE -'/,20(/,3X,A8))
  450 FORMAT (//,3X,I2,' ELEMENT VARIABLES INCLUDED IN FILE',/,3X,      
     1'REQUESTED VARIABLES INCLUDE -'/,10(/,3X,A8))
  460 FORMAT (//,3X,I2,' GLOBAL VARIABLES INCLUDED IN FILE',/,3X,       
     1'REQUESTED VARIABLES INCLUDE -'/,10(/,3X,A8))
  470 FORMAT (//,3X,'TOTAL NUMBER OF SOLUTION RECORDS (TIMEPLANES) WRITT
     1EN TO FILE - ',I4,//)
      END
